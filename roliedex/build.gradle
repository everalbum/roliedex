apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.everalbum.roliedex'
version = '1.0.1'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.0 rc1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 2
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:support-annotations:23.2.1'

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.1'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'

                // Add your description here
                name 'roliedex'
                description 'An Android View to show independently spinning digits.'
                url 'https://github.com/everalbum/roliedex'

                // Set your license
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/everalbum/roliedex/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'everalbum'
                        name 'Everalbum, Inc.'
                        email 'ralph@everalbum.com'
                    }
                }
                scm {
                    connection 'https://github.com/everalbum/roliedex'
                    developerConnection 'https://github.com/everalbum/roliedex'
                    url 'https://github.com/everalbum/roliedex'
                }
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    options {
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
    failOnError false
    source = "src/main/java"
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    from "src/main/java"
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'roliedex'
        desc = 'An Android View to show independently spinning digits.'
        websiteUrl = 'https://github.com/everalbum/roliedex'
        issueTrackerUrl = 'https://github.com/everalbum/roliedex/issues'
        vcsUrl = 'https://github.com/everalbum/roliedex.git'
        userOrg = 'everalbum'
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        version {
            name = version
            desc = 'An Android View to show independently spinning digits.'
            vcsTag = version
        }
    }
}